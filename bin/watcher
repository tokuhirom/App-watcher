#!perl
use strict;
use warnings;
use Getopt::Long;
use Filesys::Notify::Simple;
use Pod::Usage;
use App::watcher;
use 5.008008;

our $VERSION=$App::watcher::VERSION;

my @dir;
# process does not die when received SIGTERM, on win32.
my $signal=$^O eq 'MSWin32' ? 'KILL' : 'TERM';
GetOptions(
    'dir=s@'       => \@dir,
    'signal=s'     => \$signal,
    'send_only'    => \my $send_only,
    'h|help'       => \my $help,
    'e|exclude=s@' => \my @exclude,
    'v|version'    => \my $version,
) or pod2usage;
$version and do { print "watcher: $VERSION\n"; exit 0 };
pod2usage(1) if $help;
pod2usage(1) unless @ARGV;
@dir = ('.') unless @dir;

my $exclude_regex = join '|', @exclude;

sub info {
    my ( $sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst ) =
        localtime(time);
    my $time = sprintf(
        "%04d-%02d-%02dT%02d:%02d:%02d",
        $year + 1900,
        $mon + 1, $mday, $hour, $min, $sec
    );

    print "[$time] ", join(' ', @_), "\n";
}

my $pid;

sub fork_and_start {
    undef $pid;
    $pid = fork;
    die "Can't fork: $!" unless defined $pid;

    if ( $pid == 0 ) {      # child
        $SIG{INT} = $SIG{HUP} = $SIG{TERM} = 'DEFAULT';
        exec @ARGV;
        die "Cannot exec: @ARGV";
    } else {
        info("Forked process: @ARGV");
    }
}

sub kill_pid {
    $pid or return;

    info("Killing the existing process by $signal (pid:$pid)");

    kill $signal => $pid;
    waitpid( $pid, 0 );
}

sub send_signal {
    info("Sending $signal to the existing process (pid:$pid)");
    kill $signal => $pid;
}

info("watching: @dir");
fork_and_start();
exit(0) unless $pid;

my $TERM = 0;
for my $sig (qw(TERM HUP INT)) {
    $SIG{$sig} = sub {
        info("SIG$sig received");
        $TERM=1;
    };
}

my $watcher = Filesys::Notify::Simple->new(\@dir);
while(!$TERM) {
    my @restart;
    $watcher->wait(sub {
        my @events = @_;
           @events = grep { valid_file($_) } map { $_->{path} } @events;
           @events = grep { !/$exclude_regex/ } @events if $exclude_regex;
        @restart = @events;
    });
    next unless @restart;

    info("-- $_") for @restart;
    if ($send_only) {
        send_signal();
    } else {
        kill_pid();
        info("Successfully killed! Restarting the new process.");
        fork_and_start();
        unless ($pid) {
            exit(0);
        }
    }
}
if ($pid) {
    info("Terminate process: $pid");
    kill 'TERM' => $pid;
    waitpid( $pid, 0 );
}
exit 0;

sub valid_file {
    my ($file) = @_;
    $file !~ m![/\\][\._]|\.bak$|~$|_flymake\.(?:p[lm]|t)!;
}

__END__

=encoding utf8

=head1 NAME

watcher - watch the file updates

=head1 SYNOPSIS

    % watcher --dir . -- osascript -e 'tell application "Google Chrome" to reload active tab of window 1'

        --dir=.         Directory to watch.
        --signal=HUP    Sending signal to restart(Default: TERM)(EXPERIMENTAL)
        --send_only     Sending signal without fork/exec(EXPERIMENTAL)
        --exclude=file  Exclude filename(EXPERIMENTAL)
        -h --help       Show this help

=head1 DESCRIPTION

This command watches the directory updates, and run the commands.

=head1 Sending SIGHUP without restart process

(EXPERIMENTAL)

watcher can send SIGHUP without process restarting.

    % watcher --signal=HUP --send_only -- ...

=head1 AUTHOR

Tokuhiro Matsuno E<lt>tokuhirom AAJKLFJEF@ GMAIL COME<gt>

=head1 SEE ALSO

L<Filesys::Notify::Simple>

=head1 LICENSE

Copyright (C) Tokuhiro Matsuno

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

